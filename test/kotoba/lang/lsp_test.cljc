(ns kotoba.lang.lsp-test
  (:require [clojure.test :refer [deftest is testing]]
            [kotoba.lang.lsp :as lsp]))

(deftest position-range-constructors
  (is (= {:line 1 :character 2} (lsp/position 1 2)))
  (is (= {:start {:line 0 :character 0} :end {:line 0 :character 4}}
         (lsp/range (lsp/position 0 0) (lsp/position 0 4)))))

(deftest text-document-line-index
  (let [doc (lsp/text-document "abc\ndefgh\nij")]
    (is (= 3 (:line-count doc)))
    (is (= [0 4 10] (:line-starts doc)))))

(deftest position->offset
  (let [doc (lsp/text-document "abc\ndefgh\nij")]
    (is (= 0  (lsp/position->offset doc {:line 0 :character 0})))
    (is (= 2  (lsp/position->offset doc {:line 0 :character 2})))
    (is (= 5  (lsp/position->offset doc {:line 1 :character 1})))
    (is (= 12 (lsp/position->offset doc {:line 2 :character 2})))
    ;; clamp past end
    (is (= 12 (lsp/position->offset doc {:line 9 :character 9})))))

(deftest offset->position
  (let [doc (lsp/text-document "abc\ndefgh\nij")]
    (is (= {:line 0 :character 0} (lsp/offset->position doc 0)))
    (is (= {:line 0 :character 2} (lsp/offset->position doc 2)))
    (is (= {:line 1 :character 1} (lsp/offset->position doc 5)))
    (is (= {:line 2 :character 2} (lsp/offset->position doc 12)))))

(deftest offset-position-roundtrip
  (let [doc (lsp/text-document "line0\nline1\nline2")]
    (doseq [off [0 3 5 9 11 12]]
      (is (= off (lsp/position->offset doc (lsp/offset->position doc off)))))))

(deftest within-range
  (let [r (lsp/range (lsp/position 0 0) (lsp/position 0 4))]
    (is (true?  (lsp/within-range? r {:line 0 :character 0})))
    (is (true?  (lsp/within-range? r {:line 0 :character 3})))
    (is (false? (lsp/within-range? r {:line 0 :character 4})))  ; exclusive end
    (is (false? (lsp/within-range? r {:line 1 :character 0})))))

(deftest diagnostic-and-severity-sort
  (let [d-err (lsp/diagnostic (lsp/range (lsp/position 0 0) (lsp/position 0 1)) :error "parse" "bad")
        d-warn (lsp/diagnostic (lsp/range (lsp/position 0 0) (lsp/position 0 1)) :warning "lint" "odd")]
    (is (= :error (:severity d-err)))
    (is (= [d-err d-warn] (lsp/diagnostics-by-severity [d-warn d-err])))))

(deftest diagnostic-rejects-invalid-severity
  (is (thrown? #?(:clj clojure.lang.ExceptionInfo :cljs js/Error)
               (lsp/diagnostic (lsp/range (lsp/position 0 0) (lsp/position 0 1)) :fatal "x" "y"))))
